mapping = _{ SOI ~ statement ~ (NEWLINE+ ~ statement)* ~ NEWLINE* ~ EOI }

statement = _{ assignment | function | if_statement }

assignment = { target_path ~ "=" ~ query_arithmetic }

if_statement = {
    "if" ~ query_arithmetic ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" ~ ( "else" ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" )?
}

// Paths
path_index = ${ "[" ~ ('0'..'9')+ ~ "]" }
path_segment = ${ ('a'..'z' | 'A'..'Z' | "_" | "-" )+ ~ path_index? }
quoted_path_segment = ${ "\"" ~ inner_quoted_string ~ "\"" ~ path_index? }

target_path = @{ ("." ~ (path_segment | quoted_path_segment))+ }

// Functions
function = {
    deletion |
    only_fields |
    merge
}

deletion = { "del(" ~ target_paths ~ ")" }
only_fields = { "only_fields(" ~ target_paths ~ ")" }
merge = { "merge(" ~ target_path ~ "," ~ query_arithmetic ~ ("," ~ query_arithmetic)? ~ ")" }

// One or more path arguments for a given function.
//
// Can be used to parse functions that take one or more paths, e.g.:
//
// my_func = { "my_func(" ~ target_paths ~ ")" }
// => my_func(.bar, .baz)
//
target_paths = _{ target_path ~ ("," ~ target_path)* }

// Queries
path_coalesce = !{ "(" ~ (path_segment | quoted_path_segment) ~ ("|" ~ (path_segment | quoted_path_segment))+ ~ ")" }

dot_path = ${ ("." ~ (path_segment | quoted_path_segment | path_coalesce))+ }

// Query functions
//
// NOTE: Not including delete here yet, but there's no reason we can't slot it
// in at some point and get `.nested = del(.foo)`, same with `only_fields`.
query_function = {
    to_string |
    to_int |
    to_float |
    to_bool |
    to_timestamp |
    parse_timestamp |
    upcase |
    downcase |
    uuid_v4 |
    sha1 |
    md5 |
    now |
    strip_whitespace |
    truncate |
    parse_json
}

to_string = { "to_string(" ~ query_arithmetic ~ ("," ~ (string | null) )? ~ ")"}
to_int = { "to_int(" ~ query_arithmetic ~ ("," ~ (number | null) )? ~ ")"}
to_float = { "to_float(" ~ query_arithmetic ~ ("," ~ (number | null) )? ~ ")"}
to_bool = { "to_bool(" ~ query_arithmetic ~ ("," ~ (boolean | null) )? ~ ")"}
to_timestamp = { "to_timestamp(" ~ query_arithmetic ~ ("," ~ (string | number) )? ~ ")" }
parse_timestamp = { "parse_timestamp(" ~ query_arithmetic ~ "," ~ string ~ ")"}
upcase = { "upcase(" ~ query_arithmetic ~ ")"}
downcase = { "downcase(" ~ query_arithmetic ~ ")"}
uuid_v4 = { "uuid_v4()" }
sha1 = { "sha1(" ~ query_arithmetic ~ ")" }
md5 = { "md5(" ~ query_arithmetic ~ ")" }
now = { "now()" }
strip_whitespace = { "strip_whitespace(" ~ query_arithmetic ~ ")"}
truncate = { "truncate(" ~ query_arithmetic ~ "," ~ query_arithmetic ~ ("," ~ boolean)? ~ ")" }
parse_json = { "parse_json(" ~ query_arithmetic ~ ")" }

group = { "(" ~ query_arithmetic ~ ")" }

value = _{ string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "\"" ~ inner_quoted_string ~ "\"" }
inner_quoted_string = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "t")
    // TODO: Eventually expand this to allow more escape sequences.
    // | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}


not_operator = { "!" ~ query_leaf }

query_leaf = _{ not_operator | value | dot_path | group | query_function }

// Arithmetic, broken down into tiers in order to support operator precedence.
// Operators of the same tier are resolved from left to right.

// Deepest tier is the highest order of precedence, we call this just 'query' as
// it'll appear in parser error messages.
arithmetic_operator_product = { "*" | "/" | "%" }
query = { query_leaf ~ (arithmetic_operator_product ~ query_leaf)* }

arithmetic_operator_sum = { "+" | "-" }
query_arithmetic_sum = { query ~ (arithmetic_operator_sum ~ query)* }

arithmetic_operator_compare = { ">=" | ">" | "<=" | "<" | "!=" | "==" }
query_arithmetic_compare = { query_arithmetic_sum ~ (arithmetic_operator_compare ~ query_arithmetic_sum)* }

arithmetic_operator_boolean = { "||" | "&&" }
query_arithmetic_boolean = { query_arithmetic_compare ~ (arithmetic_operator_boolean ~ query_arithmetic_compare)* }

query_arithmetic = _{ query_arithmetic_boolean }

WHITESPACE = _{ " " | "\t" }
